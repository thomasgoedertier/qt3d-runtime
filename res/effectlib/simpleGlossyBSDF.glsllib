vec4 simpleGlossyBSDF( in mat3 tanFrame, in vec3 L, vec3 V, in vec3 lightSpecular, in float ior
                     , in float roughnessU, in float roughnessV, int mode )
{
  vec4 rgba = vec4( 0.0f, 0.0f, 0.0f, 1.0f );

  float cosTheta = dot( tanFrame[2], L );
  if ( 0.0f < cosTheta )
  {
    float roughness = calculateRoughness( tanFrame[2], roughnessU, roughnessV, tanFrame[0] );

    if ( ( mode == scatter_reflect ) || ( mode == scatter_reflect_transmit ) )
    {
      vec3 R = reflect( -L, tanFrame[2] );
      float cosine = dot( R, V );
      float shine = ( 0.0f < cosine ) ? ( ( 0.0f < roughness ) ? pow( cosine, 1.0f / roughness ) : ( 0.9999f <= cosine ) ? 1.0f : 0.0f ) : 0.0f;
      rgba.rgb = shine * lightSpecular;
    }
  }
  if ( ( mode == scatter_transmit ) || ( mode == scatter_reflect_transmit ) )
  {
    // check against total reflection
    vec3 R = refract( -V, tanFrame[2], ior );
    if ( R == vec3( 0.0f, 0.0f, 0.0f ) )
    {
      rgba.a = 1.0f;
    }
    else
    {
      rgba.a = 0.0f;
    }
  }

  return( rgba );
}

vec4 simpleGlossyBSDFEnvironment( in mat3 tanFrame, in vec3 viewDir, in float roughnessU, in float roughnessV, int mode )
{
  vec3 rgb = vec3( 0.0f, 0.0f, 0.0f );
#if !UIC_ENABLE_LIGHT_PROBE
  if ( uEnvironmentMappingEnabled )
  {
    float roughness = calculateRoughness( tanFrame[2], roughnessU, roughnessV, tanFrame[0] );
    vec3 R = reflect( -viewDir, tanFrame[2] );
    rgb = evalEnvironmentMap( R, roughness );
    rgb = simpleGlossyBSDF( tanFrame, R, viewDir, rgb, 1.0f, roughnessU, roughnessV, scatter_reflect ).rgb;
  }
#endif
  return( vec4( rgb, 1.0f ) );
}

// RNM radiosity normal maps
vec4 glossyRNM( in vec3 N, in vec3 rnmX, in vec3 rnmY, in vec3 rnmZ )
{
  // we use a fixed basis like Half Life
  vec3 B0 = vec3( -0.40825, 0.70711, 0.57735);
  vec3 B1 = vec3( -0.40825, -0.70711, 0.57735);
  vec3 B2 = vec3( 0.8165, 0.0, 0.57735);

  vec3 dp;
  dp.x = clamp( dot ( N , B0 ), 0.0, 1.0);
  dp.y = clamp( dot ( N , B1 ), 0.0, 1.0);
  dp.z = clamp( dot ( N , B2 ), 0.0, 1.0);

  float sum = 1.0 / dot( dp, vec3(1.0, 1.0, 1.0) );
  vec3 diffuseLight = dp.x * rnmX + dp.y * rnmY + dp.z * rnmZ;
  //vec3 diffuseLight = N.x * rnmX + N.y * rnmY + N.z * rnmZ;

  return (vec4(diffuseLight, 1.0) * sum);
}
